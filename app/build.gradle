apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.neilturner.aerialviews"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1 // Will be incremented automatically on release
        versionName "0.99.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("../AppSignature/keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties.keyAlias
                keyPassword keystoreProperties.keyPassword
                storeFile file(keystoreProperties.storeFile)
                storePassword keystoreProperties.storePassword
            }
        }
    }

    buildTypes {
        debug {
            //minifyEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled:"false", "analyticsCollectionEnabled":"false"]
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "@string/app_name_debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            //debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled:"true" , "analyticsCollectionEnabled":"true"]
            resValue "string", "app_name", "@string/app_name_release"
        }
        afterEvaluate {
            android.applicationVariants.all { variant ->
                variant.resValue 'string', 'application_id', variant.applicationId
            }
        }
    }

    flavorDimensions "version"
    productFlavors {
        create("googleplay") {
            dimension = "version"
            applicationIdSuffix = ".googleplay"
            versionNameSuffix = "-googleplay"
        }
        create("github") {
            dimension = "version"
            applicationIdSuffix = ".github"
            versionNameSuffix = "-github"
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    def coroutines_version = '1.5.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:20.0.2'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.6'
    implementation 'com.google.firebase:firebase-perf-ktx:20.0.4'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.15.1'

    implementation 'androidx.leanback:leanback:1.0.0'
    implementation 'androidx.leanback:leanback-preference:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.activity:activity-ktx:1.4.0'

    implementation 'com.chibatching.kotpref:kotpref:2.13.1'
    implementation 'com.chibatching.kotpref:initializer:2.13.1'
    implementation 'com.chibatching.kotpref:enum-support:2.13.1'

    implementation 'com.hierynomus:smbj:0.11.3'

    testImplementation 'junit:junit:4.13.2'
}